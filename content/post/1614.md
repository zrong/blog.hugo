+++
title = "【转】FLV 轉 SWF 另類作法"
postid = 1614
date = 2012-05-03T09:01:22+08:00
isCJKLanguage = true
toc = false
type = "post"
slug = "convert-flv-to-swf"
aliases = [ "/post/1614.html",]
category = [ "technology",]
tag = [ "flash", "h264", "video",]
lastmod = 2014-10-26T10:04:50+08:00
+++


本文转自：[TICORE'S BLOG][1]

網路上有很多可以將 FLV 轉為 SWF 的工具，譬如 [FLV to SWF Using FFMPEG command line][2] 不過測試發現 FPS 變得超高以下分享一個另類的做法，只要一行 Dos Command 就可以把 FLV 轉成 SWF，由於不需要重新取樣，速度非常快<!--more-->

Flash Player 10.1 之後新增 [NetStream.appendBytesAction()][3] 方法可以直接從 AS3 Binary 資料播放 Streaming 只要想辦法把 FLV 資料嵌入到 SWF，就能夠用 AS3 來播放最簡單的嵌入方式就事先寫好一個 Player SWF，然後用 Dos Command 將 FLV 資料接在後面。

FLVBytePlayer 程式碼如下：

``` actionscript
package {
 import flash.display.Loader;
 import flash.display.Sprite;
 import flash.display.StageAlign;
 import flash.display.StageScaleMode;
 import flash.events.Event;
 import flash.events.NetStatusEvent;
 import flash.media.Video;
 import flash.net.NetConnection;
 import flash.net.NetStream;
 import flash.net.NetStreamAppendBytesAction;
 import flash.net.URLLoader;
 import flash.net.URLLoaderDataFormat;
 import flash.net.URLRequest;
 import flash.text.TextField;
 import flash.ui.ContextMenu;
 import flash.utils.ByteArray;

 [SWF(width="300", height="300")]
 public class FLVBytePlayer extends Sprite {
  public var nc:NetConnection = new NetConnection();
  public var ns:NetStream;
  public var video:Video = new Video();
  public var videoWidth:Number = 1;
  public var videoHeight:Number = 1;
  public var videoByte:ByteArray;
  public var urlLdr:URLLoader = new URLLoader();
  public function FLVBytePlayer() {
   stage.scaleMode = StageScaleMode.NO_SCALE;
   stage.align = StageAlign.TOP_LEFT;
   stage.addEventListener(Event.RESIZE, onStageResizeHandler);
   contextMenu = new ContextMenu();
   contextMenu.hideBuiltInItems();
   nc.connect(null);
   ns = new NetStream(nc);
   ns.client = {
	onMetaData: function(info:Object):void{
	 trace("onMetaData:");
	 for (var i:String in info) {
	  trace("\t", i, info[i]);
	 }
	 videoWidth = info.width;
	 videoHeight = info.height;
	 onStageResizeHandler();
	}
   };
   ns.addEventListener(NetStatusEvent.NET_STATUS, onNsNetStatusHandler);
   video.smoothing = true;
   video.attachNetStream(ns);
   addChild(video);
   var ldr:Loader = new Loader();
   var req:URLRequest = new URLRequest(ldr.contentLoaderInfo.loaderURL);
   trace(ldr.contentLoaderInfo.loaderURL);
   urlLdr.dataFormat = URLLoaderDataFormat.BINARY;
   urlLdr.addEventListener(Event.COMPLETE, onLoadCompleteHandler);
   urlLdr.load(req);
  }
  public function onStageResizeHandler(e:Event = null):void{
   var stageRatio:Number = stage.stageWidth / stage.stageHeight;
   var videoRatio:Number = videoWidth / videoHeight;
   if (stageRatio > videoRatio) {
	video.height = stage.stageHeight;
	video.width = stage.stageHeight * videoRatio;
	video.y = 0;
	video.x = (stage.stageWidth - video.width) / 2;
   } else {
	video.width = stage.stageWidth;
	video.height = stage.stageWidth / videoRatio;
	video.x = 0;
	video.y = (stage.stageHeight - video.height) / 2;
   }
  }
  public function onNsNetStatusHandler(e:NetStatusEvent):void{
   trace(e.info.code);
   switch (e.info.code) {
	case "NetStream.Buffer.Empty":
	 break;
   }
  }
  public function onLoadCompleteHandler(e:Event):void{
   var data:ByteArray = urlLdr.data;
   for (var i:int = data.length - 1 ; i > 0  ; --i) {
	if (
	 data[i + 0] == 70 && /* "F".charCodeAt(0) */
	 (data[i + 1] == 76 || data[i + 1] == 52) &&
	 /* "L".charCodeAt(0) *//* "4".charCodeAt(0) */
	 data[i + 2] == 86   /* "V".charCodeAt(0) */
	) {
	 videoByte = new ByteArray();
	 data.position = i;
	 data.readBytes(videoByte, 0, 0);
	 trace(data.length, i, videoByte.length);
	 ns.play(null);
	 ns.appendBytesAction(NetStreamAppendBytesAction.RESET_SEEK);
	 ns.appendBytes(videoByte);
	 break;
	}
   }
  }
 }
}
```

以上 Player 只要編譯一次產生 SWF 後，利用以下的 Dos Command 接上 FLV就可以直接撥放了

    copy /b FLVBytePlayer.swf + Video.flv FLVBytePlayer_FLV.swf

需要注意的是 NetStream.appendBytesAction 不適用於 H264 編碼請自行斟酌使用。

[1]: http://ticore.blogspot.ca/2012/04/simple-way-convert-flv-to-swf.html
[2]: http://www.itc4u.net/it-chalk-board/67-linux-app-ffmpeg/82-flv-to-swf-using-ffmpeg-command-line.html
[3]: http://help.adobe.com/zh_TW/FlashPlatform/reference/actionscript/3/flash/net/NetStream.html#appendBytesAction()
