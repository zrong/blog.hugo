+++
title = "SWF帧频（FPS）对Socket连接的影响"
postid = 1270
date = 2011-02-24T17:31:01+08:00
isCJKLanguage = true
toc = false
type = "post"
slug = "socket_connection_on_the_impact_of_fps"
aliases = [ "/post/1270.html",]
category = [ "technology",]
tag = [ "flashplayer", "sleepmode", "socket", "swf",]
+++


[SWF帧频（FPS）对Socket连接的影响](https://blog.zengrong.net/post/1270.html)

## SWF帧频（FPS）对Socket连接的影响

Adobe官方帮助中对于Flash Player的睡眠模式有 [这样一段介绍][1] ：

> ActionScript 代码在睡眠模式下继续执行，这与将 Stage.frameRate 属性设置为 4 fps 类似。但是跳过呈现步骤，因此用户看不到该 Player 正在以 4 fps 的速率运行。之所以将帧速率选择为 4 fps （而不是 0），是因为该速率允许所有连接保持打开状态（NetStream、Socket 和 NetConnection）。将帧速率切换到 0 fps 会断开打开的连接。之所以将刷新速率选择为 250 毫秒 (4 fps)，是因为……。

确实是这样么？当FPS低于4或者为0的时候，Socket连接会断开么？低帧频的时候，通过Socket发送的数据，是根据帧率触发，还是直接触发呢？于是我写了个测试程序做了如下测试：

- 在舞台上双击的时候，可以在帧频2和帧频0之间切换；
- 每次EnterFrame的时候，向Socket服务器发送布尔值false；
- 每次单击鼠标的时候，向Socket服务器发送布尔值true；
- 在服务端，显示出每次发送数据之间的间隔，同时显示发送的数据内容（1=true，0=false）。

<!--more-->

## 结论

1. 将SWF帧频设为0的时候，Sokcet<strong><span style="color: #ff0000;">不会</span></strong>断开连接（等了十几分钟也没断开）；
1. Socket的发送请求，与帧率是没有关系的，只要发送了，就会立即执行；
1. 我每秒钟可以单击鼠标7次（这个很强悍！ :lol:）

## 服务器端的输出

>//冒号前面是客户端传送的值，后面是时间间隔（毫秒）
>
>connected
>0:15.625
>0:406.25
>0:484.375
>0:500
>0:500
>0:500
>0:500
>0:500
>0:500
>0:500
>0:500
>0:500
>0:500
>1:125
>0:156.25
>1:6796.875
>0:93187.5
>1:22328.125
>0:125
>0:500
>0:484.375
>0:500
>0:484.375
>0:515.625
>0:500
>0:484.375
>0:515.625
>1:218.75
>0:203.125

## 代码

``` actionscript
package
{
import flash.display.Sprite;
import flash.events.ErrorEvent;
import flash.events.Event;
import flash.events.IOErrorEvent;
import flash.events.MouseEvent;
import flash.events.SecurityErrorEvent;
import flash.net.Socket;

[SWF(width=300,height=200,frameRate=2)]
public class SocketFPSTest extends Sprite
{
	public function SocketFPSTest()
	{
		_socket = new Socket();
		_socket.addEventListener(Event.CONNECT, handler_connect);
		_socket.addEventListener(IOErrorEvent.IO_ERROR, handler_error);
		_socket.addEventListener(SecurityErrorEvent.SECURITY_ERROR, handler_error);
		this.stage.doubleClickEnabled = true;
		this.stage.addEventListener(MouseEvent.CLICK, handler_click);
		this.stage.addEventListener(MouseEvent.DOUBLE_CLICK, handler_doubleClick);
		this.stage.addEventListener(Event.ENTER_FRAME, handler_enterFrame);
		trace('frameRate:', this.stage.frameRate);
	}

	private var _socket:Socket;

	private function handler_error(evt:ErrorEvent):void
	{
		trace(evt.toString());
	}

	private function handler_connect(evt:Event):void
	{
		trace('连接成功！');
	}

	//每500毫秒自动发送一次消息，传送false
	private function handler_enterFrame(evt:Event):void
	{
		send(false);
		trace('enterFrame send');
	}

	//双击切换帧频
	private function handler_doubleClick(evt:MouseEvent):void
	{
		this.stage.frameRate = (this.stage.frameRate==2) ? 0 : 2;
	}

	//单击鼠标的时候发送一次消息，传送true
	private function handler_click(evt:MouseEvent):void
	{
		send(true);

	}

	private function send($value:Boolean):void
	{
		if(!_socket.connected)
			_socket.connect('127.0.0.1', 3000);
		else
		{
			_socket.writeBoolean($value);
			_socket.flush();
		}
	}
}
}
```

[1]: http://help.adobe.com/zh_CN/as3/mobile/WS4bebcd66a74275c36cfb8137124318eebc6-8000.html#WS4bebcd66a74275c36cfb8137124318eebc6-7fff
